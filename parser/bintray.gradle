apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// Bintray
Properties properties = new Properties()

if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            artifact sourcesJar
            artifact javadocJar
            from components.java

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name bintrayName
                    description libraryDescription
                    url siteUrl
                    scm {
                        url gitHubUrl
                        connection gitUrl
                        developerConnection gitSshUrl
                    }
                    licenses {
                        license {
                            name licenseName
                            url licenseUrl
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id developerId
                            name developerName
                            email developerEmail
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['maven']

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = bintrayOrg
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            name = libraryVersion
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = properties.getProperty("mavenCentral.user")
                password = properties.getProperty("mavenCentral.apikey")
                close = '1'
            }
        }
    }
}